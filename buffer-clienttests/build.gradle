plugins {
    id "com.diffplug.gradle.spotless" version "3.27.2"
    id "com.github.spotbugs" version "4.0.0"

    // Terra Test Runner Plugin
    id "bio.terra.test-runner-plugin" version "0.0.1-SNAPSHOT"
}

repositories {
    mavenCentral()
    maven {
        url "https://broadinstitute.jfrog.io/broadinstitute/libs-release-local/"
    }
    maven {
        url "https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/"
    }
}

dependencies {
    ext {
        apacheMath = '3.6.1'
        findbugsAnnotations = "3.0.1"
        guava = "29.0-jre"
        jackson = "2.10.2"
        jersey = "2.30.1"
        kubernetesClient = "8.0.2"
        logback = "1.2.3"
        slf4j = "1.7.25"
        hamcrest = "2.1"

        googleOauth2 = "0.20.0"

        bufferServiceClient = "0.4.0-SNAPSHOT"
        testRunner = "0.0.1-SNAPSHOT"
    }

    implementation group: 'org.apache.commons', name: 'commons-math3', version: "${apacheMath}"
    compileOnly "com.google.code.findbugs:annotations:${findbugsAnnotations}"
    implementation group: 'com.google.guava', name: 'guava', version: "${guava}"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jackson}"
    compile "org.glassfish.jersey.inject:jersey-hk2:${jersey}"
    compile "io.kubernetes:client-java:${kubernetesClient}"
    compile "ch.qos.logback:logback-classic:${logback}"
    compile "org.slf4j:slf4j-api:${slf4j}"
    compile "org.hamcrest:hamcrest:${hamcrest}"

    // Google dependencies
    compile "com.google.auth:google-auth-library-oauth2-http:${googleOauth2}"

    // Buffer Service client library
    implementation "bio.terra:terra-resource-buffer-client:${bufferServiceClient}"

    // Terra Test Runner library
    // Gradle project property "testrunnerjar" overrides the fetch from Maven
    if (project.hasProperty("testrunnerjar")) {
        implementation files(project.findProperty("testrunnerjar"))
    } else {
        implementation "bio.terra:terra-test-runner:${testRunner}"
    }
}

group "bio.terra"
version "1.0-SNAPSHOT"
sourceCompatibility = 1.8

spotless {
    java {
        googleJavaFormat()
    }
}
compileJava.dependsOn tasks.spotlessApply

spotbugs {
    effort = 'max'
    // This makes the "html" reports come out in plain text so you can just open the file in IntelliJ
    // and look at your bugs instead of having to switch to a browser.
    extraArgs = [ '-emacs']
}
spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main.txt")
            stylesheet = 'fancy.xsl'
        }
    }
}
spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test.txt")
            stylesheet = 'fancy.xsl'
        }
    }
}
